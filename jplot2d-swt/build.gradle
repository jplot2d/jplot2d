description = 'The SWT Composite for plot'

public String getSWTWindowingLibrary(String platform) {
    switch (platform.replaceAll(' ', '').toLowerCase()) {
        case ~/.*linux.*/: return 'gtk'
        case ~/.*darwin.*/: return 'cocoa'
        case ~/.*osx.*/: return 'cocoa'
        case ~/.*win.*/: return 'win32'
        default: return null
    }
}

public String getSWTArch(String arch) {
    switch (arch) {
        case ~/.*64.*/: return 'x86_64'
        default: return 'x86'
    }
}

public String getSWTPlatform(String platform) {
    switch (platform.replaceAll(' ', '').toLowerCase()) {
        case ~/.*linux.*/: return 'linux'
        case ~/.*darwin.*/: return 'macosx'
        case ~/.*osx.*/: return 'macosx'
        case ~/.*win.*/: return 'win32'
        default: return platform
    }
}

ext {
    swtWindowingLibrary = getSWTWindowingLibrary(System.properties['os.name'])
    swtArch = getSWTArch(System.properties['os.arch'])
    swtPlatform = getSWTPlatform(System.properties['os.name'])
    swtArtifactName = "org.eclipse.swt.${swtWindowingLibrary}.${swtPlatform}.${swtArch}"
}

dependencies {
    compile project(':jplot2d-core')
    compile project(':jplot2d-interaction')
    compile group: 'org.eclipse.swt', name: swtArtifactName, version: '4.3', transitive: false
}

//create a single Jar with all dependencies
task distJar(type: Jar, dependsOn: jar) {
    appendix = 'all'
    destinationDir = distsDir
    def deps = project(':jplot2d-core').configurations.runtime + project(':jplot2d-interaction').configurations.runtime
    from {
        deps.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}
