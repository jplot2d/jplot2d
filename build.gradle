allprojects {
    apply plugin: 'maven'

    group = 'org.jplot2d'
    version = '1.0.0'
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    repositories {
        mavenCentral()
        maven { url "http://swt-repo.googlecode.com/svn/repo/" }
    }

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.7'
        compile 'com.google.code.findbugs:jsr305:3.0.0'
        compile 'net.jcip:jcip-annotations:1.0'
        testCompile 'junit:junit:4.8.1'
        testRuntime 'org.slf4j:slf4j-simple:1.7.7'
    }

}

def exportedProjects = [
        ":jplot2d-core",
        ":jplot2d-common-interaction",
        ":jplot2d-interaction",
        ":jplot2d-swing",
        ":jplot2d-swt"
]

task docs(type: Javadoc) {
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/javadoc")
}

task distJar(dependsOn: ['jplot2d-core:zipExtLibs', ':jplot2d-swing:distJar', ':jplot2d-swt:distJar'])

task wrap(type: Wrapper) {
    gradleVersion = '2.2.1'
}
